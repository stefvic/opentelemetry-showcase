plugins {
    id 'org.springframework.boot'
    id 'io.spring.dependency-management'
    id 'idea'
}

group = 'com.stefvic.opentelemetry'
version = '1.1.0'

ext {
    versions = [
            springCloud      : '2021.0.0',
            rabbitMQ         : '5.14.1',
            testContainers   : '1.16.2',
            awaitility       : '4.1.1',
            opentTelemetryApi: '1.10.0',
            cassandraDriver  : '4.13.0'
    ]
}

subprojects {
    apply plugin: 'org.springframework.boot'
    apply plugin: 'java'
    apply plugin: 'io.spring.dependency-management'
    apply plugin: 'idea'

    dependencyManagement {
        imports {
            mavenBom "com.datastax.oss:java-driver-bom:${versions.cassandraDriver}"
            mavenBom "org.springframework.cloud:spring-cloud-dependencies:${versions.springCloud}"
            mavenBom "org.testcontainers:testcontainers-bom:${versions.testContainers}"
            mavenBom "io.opentelemetry:opentelemetry-bom:${versions.opentTelemetryApi}"
        }
        dependencies {
            dependency "org.awaitility:awaitility:${versions.awaitility}"
            dependency "com.rabbitmq:amqp-client:${versions.rabbitMQ}"
        }
    }

    configurations {
        compileOnly {
            extendsFrom annotationProcessor
        }
        testCompileOnly {
            extendsFrom annotationProcessor
        }
    }

    java {
        toolchain {
            languageVersion = JavaLanguageVersion.of(17)
        }
        withJavadocJar()
        withSourcesJar()
    }

    javadoc {
        options.addStringOption('Xdoclint:none', '-quiet')
    }

    test {
        useJUnitPlatform()
    }

    dependencies {
        compileOnly 'org.projectlombok:lombok'
        testCompileOnly 'org.projectlombok:lombok'
        annotationProcessor 'org.projectlombok:lombok'
        testAnnotationProcessor 'org.projectlombok:lombok'
    }
}


